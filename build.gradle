buildscript {
    repositories { jcenter() }

    dependencies {
        classpath 'com.netflix.nebula:gradle-aggregate-javadocs-plugin:2.2.+'
    }
}

apply plugin: 'nebula-aggregate-javadocs'


allprojects {
    version = "0.1.0-SNAPSHOT"
}

subprojects {

    apply plugin: 'groovy'
    apply plugin: 'maven'
    apply plugin: 'java'
    apply plugin: 'jacoco'

    apply from: "https://raw.githubusercontent.com/ametiste-oss/ametiste-bintray-gradle/master/bintray-ext-ame-oss.gradle"
    apply from: "https://raw.githubusercontent.com/ametiste-oss/ametiste-bintray-gradle/master/bintray-ext.gradle"

    apply plugin: 'maven-publish'

    group = 'org.ametiste.utils'

    ext {

        integrationVersion = "4.2.0.RELEASE"
        springBootVersion = "1.2.5.RELEASE"
        springVersion = "4.2.4.RELEASE"
        bintray_vcsUrl = 'https://github.com/ametiste-oss/ametiste-utils.git'

    }

    configurations.all {
        // check for updates every build
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        testCompile 'junit:junit:4.11'
        testCompile 'org.mockito:mockito-all:1.9.5'
        compile "org.slf4j:slf4j-api:1.7.7"
    }

    configurations {
        deployerJars
    }

    javadoc {
        options {
            encoding = 'UTF-8'
        }
    }

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    task sourcesJar(type: Jar, dependsOn:classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn:javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    test.outputs.upToDateWhen {false}
    test.testLogging.showStandardStreams = true

    test {
        afterTest { desc, result ->
            println "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
        }
    }

}

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}

configure(rootProject) {

    description = 'Ametiste utils'

}
